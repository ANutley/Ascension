import dev.vankka.dependencydownload.task.GenerateDependencyDownloadResourceTask

[
        'net.kyori',
        'me.lucko.commodore'
].each {
    tasks.shadowJar.relocate it, 'com.discordsrv.dependencies.' + it
    tasks.generateRuntimeDownloadResourceForRuntimeDownloadOnly.relocate it, 'com.discordsrv.dependencies.' + it
}
// More relocations in buildscript/relocations.gradle

shadowJar {
    archiveFileName = 'bukkit.jarinjar'
}

apply from: rootProject.file('buildscript/runtime.gradle')

configurations {
    commodore
    compileOnly.extendsFrom commodore
}

tasks.register('generateResourceForCommodore', GenerateDependencyDownloadResourceTask) {
    var conf = configurations.commodore
    configuration = conf
    file = 'dependencies/' + conf.name + '.txt'
}

allprojects {
    repositories {
        exclusiveContent {
            forRepository {
                maven { url 'https://papermc.io/repo/repository/maven-public/' }
            }
            filter {
                includeGroup 'com.destroystokyo.paper'
                includeGroup 'io.papermc.paper'
                includeGroup 'dev.folia'
                includeGroup 'org.spigotmc'
                includeGroup 'net.md-5'
            }
        }

        exclusiveContent {
            forRepository {
                maven { url 'https://nexus.scarsz.me/content/groups/public/' }
            }
            filter {
                includeGroup 'net.milkbowl.vault'
                includeGroup 'me.clip'
                includeGroup 'com.palmergames.bukkit'
                includeGroup 'mineverse.aust1n46'
                includeGroup 'ru.mrbrikster'
                includeGroup 'com.github.ucchyocean.lc'
            }
        }
    }
}

dependencies {
    // API
    annotationProcessor project(':api')

    // Platform
    compileOnly(libs.spigotapi)

    // Common
    compileOnly project(':common')
    implementation project(path: ':common', configuration: 'runtimeElements')

    // Folia, modern bukkit
    api project(':bukkit:bukkit-folia')
    api project(':bukkit:bukkit-paper')
    api project(':bukkit:bukkit-bukkit1_12')

    // DependencyDownload
    implementation(libs.mcdependencydownload.bukkit.bootstrap)

    // Adventure
    runtimeDownloadApi(libs.adventure.api)
    runtimeDownloadApi(libs.adventure.platform.bukkit)

    // Commodore
    commodore(libs.commodore) {
        // We only use commodore when it's included in the server, so we don't want to download it
        exclude module: 'brigadier'
        // We don't use the commodore file format
        exclude module: 'commodore-file'
    }

    // Integrations
    compileOnly(libs.vaultapi)
    compileOnly(libs.placeholderapi.bukkit)

    // Chat Integrations
    compileOnly(libs.chatty)
    compileOnly(libs.lunachat)
    compileOnly(libs.townychat)
    compileOnly(libs.venturechat)
}


processResources {
    dependsOn(generateResourceForCommodore)
}