import dev.vankka.dependencydownload.task.GenerateDependencyDownloadResourceTask

apply from: rootProject.file('buildscript/runtime.gradle')

configurations {
    commodore
    compileOnly.extendsFrom commodore
}

task generateResourceForCommodore(type: GenerateDependencyDownloadResourceTask) {
    var conf = configurations.commodore
    configuration = conf
    file = 'dependencies/' + conf.name + '.txt'
}

shadowJar {
    archiveFileName = 'bukkit.jarinjar'

    [
            'net.kyori',
            'me.lucko.commodore'
    ].each {
        relocate it, 'com.discordsrv.dependencies.' + it
    }
    // More relocations in buildscript/relocations.gradle
}

allprojects {
    repositories {
        exclusiveContent {
            forRepository {
                maven { url 'https://papermc.io/repo/repository/maven-public/' }
            }
            filter {
                includeGroup 'com.destroystokyo.paper'
            }
        }

        exclusiveContent {
            forRepository {
                maven { url 'https://nexus.scarsz.me/content/groups/public/' }
            }
            filter {
                includeGroup 'net.milkbowl.vault'
            }
        }
    }

    dependencies {
        // Platform
        compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    }
}

dependencies {
    // API
    annotationProcessor project(':api')

    // Common
    compileOnly project(':common:common-server')
    implementation project(path: ':common:common-server', configuration: 'runtimeElements')

    // DependencyDownload
    implementation 'dev.vankka:minecraftdependencydownload-bukkit:' + rootProject.mddVersion

    // Adventure
    runtimeDownloadApi 'net.kyori:adventure-platform-bukkit:' + rootProject.adventurePlatformVersion

    // Commodore
    commodore('me.lucko:commodore:1.10') {
        // We only use commodore when it's included in the server, so we don't want to download it
        exclude module: 'brigadier'
        // We don't use the commodore file format
        exclude module: 'commodore-file'
    }

    // Integrations
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
}

processResources {
    dependsOn(generateResourceForCommodore)
}
