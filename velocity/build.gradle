apply from: rootProject.file('buildscript/standalone.gradle')
apply plugin: 'net.kyori.blossom'

shadowJar {
    archiveBaseName = 'DiscordSRV-Velocity'

    // Relocations in buildscript/relocations.gradle
}

repositories {
    exclusiveContent {
        forRepository {
            maven { url 'https://nexus.velocitypowered.com/repository/maven-public/' }
        }
        filter {
            includeGroup 'com.velocitypowered'
        }
    }
}

dependencies {
    // API
    annotationProcessor project(':api')
    implementation project(':common:common-api')

    // Common
    implementation project(':common')
    implementation project(':common:common-templates')

    // Platform
    annotationProcessor(libs.velocity)
    compileOnly(libs.velocity)

    // DependencyDownload
    implementation(libs.mcdependencydownload.velocity)
}

jar {
    dependsOn blossomSourceReplacementJava
}

blossom {
    var mainClass = 'src/main/java/com/discordsrv/velocity/DiscordSRVVelocityBootstrap.java'
    replaceToken('@VERSION@', project.version, mainClass)
}
