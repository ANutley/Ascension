apply from: rootProject.file('buildscript/runtime.gradle')

shadowJar {
    archiveFileName = 'bungee.jarinjar'

    [
            'net.kyori'
    ].each {
        relocate it, 'com.discordsrv.dependencies.' + it
    }
    // More relocations in buildscript/relocations.gradle
}

allprojects {
    repositories {
        exclusiveContent {
            forRepository {
                maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
            }
            filter {
                includeGroup 'net.md-5'
            }
        }
    }

    dependencies {
        // Platform
        compileOnly 'net.md-5:bungeecord-api:1.17-R0.1-SNAPSHOT'
    }
}

dependencies {
    // API
    annotationProcessor project(':api')

    // Common
    implementation project(':common:common-proxy')

    // DependencyDownload
    implementation 'dev.vankka:minecraftdependencydownload-bungee:' + rootProject.mddVersion

    // Adventure
    runtimeDownloadApi 'net.kyori:adventure-platform-bungeecord:' + rootProject.adventurePlatformVersion
    runtimeDownloadApi 'net.kyori:adventure-text-serializer-bungeecord:' + rootProject.adventurePlatformVersion
}
